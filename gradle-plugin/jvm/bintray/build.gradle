// SPDX-Copyright: Â©  Basil Peace
// SPDX-License-Identifier: Apache-2.0
plugins {
  id 'org.fidata.project.java'
  id 'org.fidata.project.groovy'
  id 'org.fidata.plugin'
}
publicReleases = true // TODO

group = "${ parent.group }.${ name.replace('-', '_') }" // TODO

ext.rootDir = file('.')

dependencies {
  implementation(project(':gradle-plugin:jvm:maven'))
  implementation(group: 'com.jfrog.bintray', name: 'com.jfrog.bintray.gradle.plugin', version: '[1, 2[') {
    /*
     * WORKAROUND:
     * org.apache.maven:maven-ant-tasks has old plexus dependency which have undesired JUnit dependency
     * <grv87 2018-06-24>
     */
    exclude group: 'org.codehaus.plexus', module: 'plexus-container-default'
  }
  implementation(group: 'org.codehaus.plexus', name: 'plexus-container-default', version: '[1, 2[') {
    /*
     * WORKAROUND:
     * org.codehaus.plexus:plexus-container-default still has google-collections dependency
     * which is superseded by Guava
     * <grv87 2018-06-24>
     */
    exclude group: 'com.google.collections', module: 'google-collections'
  }
  testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '[1, 2['
}

gradlePlugin {
  plugins {
    aboutBintrayPlugin {
      id = 'org.fidata.about.jvm.bintray'
      implementationClass = 'org.fidata.about.gradle.jvm.AboutBintrayPlugin'
    }
  }
}

stutter {
  java(8) {
    compatibleRange '2.10'
  }
  java(9) {
    compatibleRange '2.10'
  }
}

tasks.named('codenarcCompatTest').configure {
  disabledRules.add 'JavaIoPackageAccess' // TODO
}

pluginBundle.plugins {
  aboutBintrayPlugin {
    // TODO displayName = 'Integration plugin between About and Bintray plugins'
  }
}
